! local concat = newline

> topic new_user

	+ *
	- Hi! I am your personal Dieting Chatbot!
        ^ First, I need to ask you a few questions about your physical information.
        ^ What is your name?<set state=collect_user_info> <set img_received=false> <set url_received=false> {topic=collect_user_name}

< topic

///////////////////////////////////////standby

> topic standby

    // don't know what user said and tell them what can we do
	+ *
	- Sorry, I'm not sure how to respond to that.
    ^ Please tell me what you want to do :P
    ^ * Input a food menu and get food recommendation
    ^ * Record what you have eaten last meal
    ^ * Update personal information
    ^ * Ask for dieting history

	+ [*] google *
	- Google results for "<star>": <a href="http://google.com/search?q=<star>">Click Here</a>

    // trigger to collect_user_info state
    + [*] ((tweak|change|alter|update|modify|adjust)|(info|information|details|particulars|name|age|gender|height|weight|allergy|allergies)) [*]
    - Do you want to update your personal information? {topic=collect_user_info_confirmation}

    // trigger to input_menu state (possibly go to recommend state later)
	+ [*] (input|eating|restaurant|help me|look|check out|menu|recommend|recommendation|suggest|suggestion|decide|should|what to eat) [*]
	- Please let me have a look at your menu first. You can either take a photo, or share the menu url with me. {topic=input_menu} <set state=input_menu>

    // trigger to record what the user ate
    + [*] (ate|have eaten|has eaten|had eaten|record|last meal|finished|finish|@meals) [*]
    - Please tell me what you ate. {topic=post_eating} <set state=post_eating>

// implement later
//    // trigger to query dieting history
//	+ [*] (what [did|had] i (had|eat|eaten) (yesterday|just now|not long ago)|what is my (fat|carbohydrate|sodium|vitamin) intake) [*]
//	- Please wait while I check your eating history.
//		^ {topic=provide_info} <set state=provide_info>

	+ [*] (talk later|see you|bye) [*]
	- Okay see you later.
< topic

// still in standby state
> topic collect_user_info_confirmation

    + @yes [*]
    - OK. What is your name? {topic=collect_user_age} <set state=collect_user_info>

    + @no [*]
    - OK. Tell me when you need help~~~~ {topic=standby}

    + *
    - {topic=standby}{@ <star>}
< topic

///////////////////////////////////////collect_user_info



> topic collect_user_name

    + (i am|my name is|this is) *
    - <set name={formal}<star2>{/formal}> Is {formal}<star2>{/formal} your name? {topic=collect_user_name_confirmation}

    + *
    - <set name={formal}<star>{/formal}> Is {formal}<star>{/formal} your name? {topic=collect_user_name_confirmation}

< topic
> topic collect_user_name_confirmation

    + @yes [*]
    - Ok. Nice to meet you <get name>! Next question! What is your age? {topic=collect_user_age}

    + *
    - Can you enter your name again? Pls~ {topic=collect_user_name}

< topic



> topic collect_user_age

    + [*] # [*]
    - <set age=<star>>Are you <star> years old? {topic=collect_user_age_confirmation}

    + *
    - I do not understand. Can you please input a number? What is your age?

< topic
> topic collect_user_age_confirmation

    + @yes [*]
    - Alright, what is your gender? {topic=collect_user_gender}

    + @no [*]
    - Can you enter your age again? Pls~ {topic=collect_user_age}

    + *
    - I don't understand. Are you <get age> years old?

< topic



> topic collect_user_gender

    + [i] [am] [a] (@malenoun)
    - <set gender=male>You are a <star>. Ok, so what is your weight (in kg)? Please input an integer. {topic=collect_user_weight}

    + [i] [am] [a] (@femalenoun)
    - <set gender=female>You are a <star>. Ok, so what is your weight (in kg)? Please input an integer. {topic=collect_user_weight}

    + *
    - I am sorry. I do not understand. What is your gender?

< topic



> topic collect_user_weight

    + [*] # [*]
    - <set weight=<star>>Is your weight <star>kg? {topic=collect_user_weight_confirmation}

    + *
    - I am sorry. I do not understand. Can you input a number? What is your weight (in kg)? Please input an integer.

< topic
> topic collect_user_weight_confirmation

    + @yes [*]
    - Alright, then what is your height in cm?{topic=collect_user_height}

    + @no [*]
    - Can you enter your weight again? Please input an integer.

    + *
    - I don't understand. Is your weight <get weight>kg?
    
< topic


> topic collect_user_height

	+ [*] # [*]
    - <set height=<star>>Is your height <star>cm? {topic=collect_user_height_confirmation}

    + *
    - I do not understand. Can you please input a number? What is your height (in cm)? Please input an integer.

< topic
> topic collect_user_height_confirmation

    + @yes [*]
    - Great. Are you allergic to milk? (Yes/No){topic=collect_user_allergy_milk}

    + @no [*]
    - Can you enter your height (in cm) again? Please input an integer. {topic=collect_user_height}

    + *
    - I don't understand. Is your height <get height>cm?
    
< topic



>topic collect_user_allergy_milk

    + @yes [*]
    - <set milk_allergy=true>I see, I'll take note of that. Are you allergic to eggs? (Yes/No) {topic=collect_user_allergy_eggs}


    + @no [*]
    - <set milk_allergy=false>Great, are you allergic to eggs? (Yes/No) {topic=collect_user_allergy_eggs}

    + *
    - I am sorry. I do not understand. Are you allergic to milk? (Yes/No)

< topic



>topic collect_user_allergy_eggs

    + @yes [*]
    - <set egg_allergy=true>I see, I'll take note of that. Are you allergic to nuts in general? (Yes/No) {topic=collect_user_allergy_nuts}


    + @no [*]
    - <set egg_allergy=false>Great, are you allergic to nuts in general? (Yes/No) {topic=collect_user_allergy_nuts}

    + *
    - I am sorry. I do not understand. Are you allergic to eggs? (Yes/No)

< topic



>topic collect_user_allergy_nuts

    + @yes [*]
    - <set nut_allergy=true>I see, I'll take note of that. Are you allergic to seafood? (Yes/No) {topic=collect_user_allergy_seafood}


    + @no [*]
    - <set nut_allergy=false>Great, are you allergic to seafood? (Yes/No) {topic=collect_user_allergy_seafood}

    + *
    - I am sorry. I do not understand. Are you allergic to nuts? (Yes/No)

< topic



>topic collect_user_allergy_seafood

    + @yes [*]
    - <set seafood_allergy=true>I see, I'll take note of that. Thank you for your cooperation, I have a better understanding of your physical conditions now. <set state=standby>{topic=standby}


    + @no [*]
    - <set seafood_allergy=false>Great, thanks for cooperating. I have a better understanding of your physical condition now. <set state=standby>{topic=standby}

    + *
    - I am sorry. I do not understand. Are you allergic to seafood? (Yes/No)

< topic



//////////////////////////////////////collect_user_info


> topic input_menu 
	+ [*] (talk [to you] later|leave) [*]
	- Okay see you later.
		^ {topic=standby} <set state=standby>
    
	+ * 
	* <get img_received> == true => Thanks, I'm looking at your photo now! I'll try to give you some recommendations.
	* <get url_received> == true => Thanks, I'm looking at your url now! I'll try to give you some recommendations.
	- I don't understand. Can you please provide me with a photo or url of your menu?
	
< topic



> topic post_eating
	+ talk later
	- Okay see you later.
	^ {topic=standby} <set state=standby>

	+ * 
	- post_eating
< topic



//> topic provide_info
//    + [*] (talk [to you] later|leave) [*]
//    - Okay see you later.
//		^ {topic=standby} <set state=standby>
//    
//	+ *
//	- provide_info
//< topic



> topic recommend

    + [*] (talk [to you] later|leave) [*]
    - Okay see you later.
		^ {topic=standby} <set state=standby>

< topic

